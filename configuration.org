#+TIxTLE: Emacs Configuration
#+DESCRIPTION: My literate emacs configuration using org-mode.
#+AUTHOR: Michael Zappa

init.el uses org-babel-load file to automatically load in the emacs-lisp
blocks. Emacs 27.x. Only tested on PopOS, that should only be relevant for EXWM (and might not even be).

- TODO put hydras with their topic
- TODO evaluate need for treemacs/actually making it work as desired (opens current project only)
- TODO go through https://github.com/daviwil/dotfiles/blob/master/Emacs.org
- emacs from scratch repo: https://github.com/daviwil/emacs-from-scratch/blob/master/show-notes/Emacs-Desktop-01.org
* Things to Install
** Apt Packages (or equivalent)
- silversearcher-ag (projectile-ag)
- clangd (C lsp-mode)
- texlive-latex-extra, texlive-extra-utils (AUCTeX)
- libpng-dev zlib1g-dev libpoppler-glib-dev libpoppler-private-devm imagemagick (pdf-tools)
- nodejs (tide-mode)
- playerctl, brightnessctl (exwm)
- cmake, libtool-bin, libvterm-dev (vterm)
- slock (exwm)
- compton or equivalent compositor (exwm)
- npm
** Other Sources
- Elixir Language Server:  https://github.com/elixir-lsp/elixir-ls.
- OCaml Language Server:  https://github.com/ocaml/ocaml-lsp
- Python Language Server:  https://github.com/palantir/python-language-server
- Rust Language Server:  https://github.com/rust-lang/rls
* Packages Setup
  Setting up the package archives and the lisp to automatically load all my packages.
  #+begin_src emacs-lisp
    ;; setting up the MELPA and other repos
    (require 'package)
    (add-to-list 'package-archives '("gnu" . "https://elpa.gnu.org/packages/") t)
    (add-to-list 'package-archives '("melpa" . "https://stable.melpa.org/packages/") t)
    (add-to-list 'package-archives '("melpa-unstable" . "https://melpa.org/packages/") t)
    (add-to-list 'package-archives '("org" . "http://orgmode.org/elpa/") t)


    (setq package-archive-priorities
          '(("melpa" . 50)
            ("gnu" . 10)
            ("org" . 10)
            ("melpa-unstable" . 0)))

    ;; install use-package if not installed
    (when (not (package-installed-p 'use-package))
      (package-refresh-contents)
      (package-install 'use-package))

    (unless package-archive-contents
      (package-refresh-contents))

    ;; ensure use-package by default
    (require 'use-package-ensure)
    (setq use-package-always-ensure t)

    ;; compile packages
    (use-package auto-compile
      :config (auto-compile-on-load-mode))

    ;; use newest version of packages
    (setq load-prefer-newer t)
  #+end_src
* Personal
  Giving emacs my information.
  #+begin_src emacs-lisp
    (setq user-emacs-directory "~/.emacs.d/user-dir")
    (setq custom-file          "~/.emacs.d/custom.el")
    (setq bookmark-default-file "~/.emacs.d/bookmarks")
    (setq abbrev-file-name     "~/.emacs.d/abbrev_defs")

    (setq user-full-name       "Michael Zappa")
    (setq user-mail-address    "zapprich@gmail.com")
  #+end_src
* Misc
  Miscellanious things which might be put somewhere else eventually.
  #+begin_src emacs-lisp
    ;; scroll compilation output in the window
    (setq compilation-scroll-output t)

    ;; minor mode for inserting parens, braces, quotes and the like in pairs.
    ;;turned "<> off for org mode since it makes the shortcut for org blocks difficult."
    (electric-pair-mode)
    (add-hook 'org-mode-hook
              (lambda () (setq-local electric-pair-inhibit-predicate
                                (lambda (c)
                                  (if (eq c ?<)
                                      t
                                    (electric-pair-inhibit-predicate c))))))

    ;; pretty symbols like lambda
    (global-prettify-symbols-mode t)

    ;; reduce the frequency of garbage collection by making it happen on
    ;; each 50MB of allocated data (the default is on every 0.76MB)
    (setq gc-cons-threshold 50000000)

    ;; warn when opening files bigger than 100MB
    (setq large-file-warning-threshold 100000000)

    ;; quit Emacs directly even if there are running processes
    (setq confirm-kill-processes nil)

    ;; store all backup and autosave files in the tmp dir
    (setq backup-directory-alist
          `((".*" . ,temporary-file-directory)))
    (setq auto-save-file-name-transforms
          `((".*" ,temporary-file-directory t)))

    ;; Add color formatting to *compilation* buffer
    (add-hook 'compilation-filter-hook
              (lambda () (ansi-color-apply-on-region (point-min) (point-max))))
  #+end_src
* TRAMP
  #+begin_src emacs-lisp
    (require 'tramp)
    (setq tramp-default-method "ssh")

    ;; helper function to sudo a file
    (defun sudo ()
      "Use TRAMP to `sudo' the current buffer"
      (interactive)
      (when buffer-file-name
        (find-alternate-file
         (concat "/sudo:root@localhost:"
                 buffer-file-name))))

    ;; attempt to speed things up
    (defadvice projectile-project-root (around ignore-remote first activate)
      (unless (file-remote-p default-directory) ad-do-it))
    (setq remote-file-name-inhibit-cache nil)
    (setq vc-ignore-dir-regexp
          (format "%s\\|%s"
                  vc-ignore-dir-regexp
                  tramp-file-name-regexp))
    (setq tramp-verbose 1)

    (add-to-list 'tramp-remote-path "~/.local/bin")
  #+end_src
* Mouse and Keys
  Mouse and keyboard settings which don't belong to any specific package or function.
  #+begin_src emacs-lisp
    ;; scrolling
    (setq scroll-margin 0
          scroll-conservatively 100000
          scroll-preserve-screen-position 1)

    ;; change font size binding
    (global-set-key (kbd "C-+") 'text-scale-increase)
    (global-set-key (kbd "C--") 'text-scale-decrease)

    ;; shortcut for copying lines using verbose syntax
    (global-set-key (kbd "C-c D") "\C-a\C- \C-n\M-w")

    ;; keybinding to reload configuration
    (global-set-key (kbd "C-c m") (lambda () (interactive) (load-file "~/.emacs.d/init.el")))

    ;; keybinding to open configuration file (this file)
    (global-set-key (kbd "C-c n") (lambda ()  (interactive) (find-file "~/.emacs.d/configuration.org")))

    ;; assume I want to close current buffer with ""C-x k""
    (global-set-key (kbd "C-x k") (lambda () (interactive) (kill-buffer (current-buffer))))

    ;; replacing keybinding for undo as it is "C-/" by default and I use that for toggling comments.
    (global-set-key (kbd "C-.") 'undo)
  #+end_src
* Hydra
  Hydra provides the ability to create a keybinding menu to reduce redundant keypresses.
  #+begin_src emacs-lisp
    (use-package hydra
      :bind
      (("C-c f" . hydra-formatting/body)
       ("C-x t" . hydra-tab-bar/body)
       ("C-c l" . hydra-lsp/body)
       ("C-c o" . hydra-org/body)
       ("C-c p" . hydra-projectile/body)
       ("C-x w" . hydra-windmove/body)))
  #+end_src
* User Interface
  Packages and configuration related to modifying the user-interface.
** all-the-icons
  #+begin_src emacs-lisp
    ;; font for more emacs icons
    (use-package all-the-icons)
  #+end_src
** minions
  #+begin_src emacs-lisp
    ;; turns off all minor modes in modeline
    (use-package minions
      :config
      (setq minions-mode-line-lighter ""
            minions-mode-line-delimiters '("" . ""))
      (minions-mode 1))
  #+end_src
** Startup Configuration
  #+begin_src emacs-lisp
    ;; scratch screen
    (setq inhibit-startup-screen t)
    (setq initial-scratch-message "")

    ;; hecking bell
    (setq ring-bell-function 'ignore)

    ;; shallow tabs
    (setq-default tab-width 2)

    ;; turn off things
    (menu-bar-mode -1)
    (scroll-bar-mode -1)
    (tool-bar-mode -1)
    (blink-cursor-mode -1)

    ;; initial frame maximized
    (add-to-list 'initial-frame-alist '(fullscreen . maximized))
  #+end_src
** Theme Configuration
  #+begin_src emacs-lisp
    ;; ;; nord-theme, doesn't have tab-bar support yet
    ;; (use-package nord-theme
    ;;  :config
    ;;  (load-theme 'nord t))

    ;; dracula-theme, which has tab-bar support
    ;; (use-package dracula-theme
    ;;   :config
    ;;   (load-theme 'dracula t))

    ;; modus-vivendi, dark theme from prot
    (use-package modus-vivendi-theme
      :config
      (load-theme 'modus-vivendi t))
  #+end_src
** Frames Configuration
  #+begin_src emacs-lisp
    ;;more useful frame title, that show either a file or a
    ;; buffer name (if the buffer isn't visiting a file)
    (setq frame-title-format '((:eval (projectile-project-name))))

    ;; line numbers, column number, size indication
    (global-display-line-numbers-mode)
    (line-number-mode t)
    (column-number-mode t)
    (size-indication-mode t)

    ;; winner-mode to undo and redo window configurations
    (winner-mode)
  #+end_src
** tab-bar-mode
   #+begin_src emacs-lisp
     (tab-bar-mode)
     (global-set-key (kbd "M-[") 'tab-bar-history-back)
     (global-set-key (kbd "M-]") 'tab-bar-history-forward)
     ;; hydra bindings for tab-bar-mode
     (defhydra hydra-tab-bar (:color amaranth)
       "Tab Bar Operations"
       ("t" tab-new "Create a new tab" :column "Creation")
       ("d" dired-other-tab "Open Dired in another tab")
       ("f" find-file-other-tab "Find file in another tab")
       ("0" tab-close "Close current tab")
       ("m" tab-move "Move current tab" :column "Management")
       ("r" tab-rename "Rename Tab")
       ("<return>" tab-bar-select-tab-by-name "Select tab by name" :column "Navigation")
       ("<right>" tab-next "Next Tab")
       ("<left>" tab-previous "Previous Tab")
       ("q" nil "exit" :color blue))
   #+end_src
* User Interaction
  Packages and configuration related to user interaction.
** avy
  #+begin_src emacs-lisp
    ;skip around the screen
    (use-package avy
      :init
      (global-set-key (kbd "C-;") 'avy-goto-char-2))
  #+end_src
** counsel
  #+begin_src emacs-lisp
    ;; autocomplete interface for search
    (use-package counsel
      :bind
      (("C-s" . swiper)
       ("C-x C-r" . counsel-recentf))
      :config
      (setcdr (assoc 'counsel-M-x ivy-initial-inputs-alist) "") ;; not only prefixes
      (ivy-mode)
      (counsel-mode)
      (use-package ivy-hydra))
  #+end_src
** exec-path-from-shell
  #+begin_src emacs-lisp
    ;; Force Emacs to use shell path
    (use-package exec-path-from-shell
      :config
      (exec-path-from-shell-initialize))
  #+end_src
** smex
  #+begin_src emacs-lisp
    ;; frequency sorter to integrate with counsel
    (use-package smex)
  #+end_src
** which-key
  #+begin_src emacs-lisp
    ;; shows possible key combinations
    (use-package which-key
      :config
      (which-key-mode))
  #+end_src
** windmove
  #+begin_src emacs-lisp
    (use-package windmove
      ;; default keybindings are S-s-<direction>, but super doesn't get past GNOME shell
      :bind
      (("C-S-<left>" . windmove-swap-states-left)
       ("C-S-<right>" . windmove-swap-states-right)
       ("C-S-<up>" . windmove-swap-states-up)
       ("C-S-<down>" . windmove-swap-states-down))
      :config
      ;; use shift + arrow keys to switch between visible buffers
      (windmove-default-keybindings)

      ;; Make windmove work in org-mode.
      (add-hook 'org-shiftup-final-hook 'windmove-up)
      (add-hook 'org-shiftleft-final-hook 'windmove-left)
      (add-hook 'org-shiftdown-final-hook 'windmove-down)
      (add-hook 'org-shiftright-final-hook 'windmove-right)

      (add-hook 'org-shiftcontrolup-final-hook 'windmove-swap-states-up)
      (add-hook 'org-shiftcontrolleft-final-hook 'windmove-swap-states-left)
      (add-hook 'org-shiftcontroldown-final-hook 'windmove-swap-states-down)
      (add-hook 'org-shiftcontrolright-final-hook 'windmove-swap-states-right))

    (defhydra hydra-windmove (:color amaranth)
      "Windmove Operations"
      ("<left>" windmove-left "left" :column "Change window")
      ("<right>" windmove-right "right")
      ("<up>" windmove-up "up")
      ("<down>" windmove-down "down")

      ("C-<left>" windmove-swap-states-left "move left" :column "Move window")
      ("C-<right>" windmove-swap-states-right "move right")
      ("C-<up>" windmove-swap-states-up "move up")
      ("C-<down>" windmove-swap-states-down "move down")

      ("q" nil "exit" :color blue))
  #+end_src
** Yes/No Question Configuration
   #+begin_src emacs-lisp
     ;; enable y/n answers
     (fset 'yes-or-no-p 'y-or-n-p)
   #+end_src
* VTerm
  Preferred emacs terminal emulator.
  #+begin_src emacs-lisp
    (use-package vterm)
  #+end_src
* Project Management
  Packages and configuration related to managing projects.
** magit
  #+begin_src emacs-lisp
    ;; magit git interface
    (use-package magit
      :config
      (setq magit-completing-read-function 'ivy-completing-read))
  #+end_src
** projectile
  #+begin_src emacs-lisp
    ;; project manager
    (use-package projectile
      :init
      (setq projectile-completion-system 'ivy)
      (setq projectile-project-search-path '("~/Projects"))
      (use-package ag)
      (use-package ibuffer-projectile)
      :config
      (projectile-mode +1)
      (setq projectile-mode-line "Projectile"))

    ;; hydra bindings for projectile
    (defhydra hydra-projectile-other-window (:color amaranth)
      "projectile-other-window"
      ("f"  projectile-find-file-other-window        "file" :column "Find File")
      ("g"  projectile-find-file-dwim-other-window   "file dwim")
      ("d"  projectile-find-dir-other-window         "dir")
      ("b"  projectile-switch-to-buffer-other-window "buffer")
      ("q"  nil                                      "cancel" :color blue))

    (defhydra hydra-projectile (:color amaranth)
      "PROJECTILE: %(projectile-project-root)"

      ("ff"  projectile-find-file "file" :column "Find File")
      ("s-f" projectile-find-file-dwim "file dwim")
      ("fd"  projectile-find-file-in-directory "file curr dir")
      ("r"   projectile-recentf "recent file")
      ("d"   projectile-find-dir "dir")

      ("b"   projectile-switch-to-buffer "switch to buffer" :column "Buffers")
      ("i"   projectile-ibuffer "ibuffer")
      ("K"   projectile-kill-buffers "kill all buffers")

      ("c"   projectile-invalidate-cache "clear cache" :column "Cache (danger)")
      ("x"   projectile-remove-known-project "remove known project")
      ("X"   projectile-cleanup-known-projects "cleanup projects")
      ("z"   projectile-cache-current-file "cache current project")

      ("a"   projectile-ag "ag" :column "Project")
      ("p"   projectile-switch-project "switch project" :column "Project")
      ("P"   treemacs-projectile "treemacs")

      ("`"   hydra-projectile-other-window/body "other window" :color blue :column "Other")
      ("q"   nil "exit" :color blue))
  #+end_src
** treemacs
  #+begin_src emacs-lisp
    ;; sidebar file explorer
    (use-package treemacs
      :bind
      ("C-c t" . treemacs)
      :commands
      (treemacs-filewatch-mode
       treemacs-git-mode
       treemacs-follow-mode)
      :config
      (setq treemacs-width 24)
      (add-hook 'treemacs-mode-hook (lambda() (display-line-numbers-mode -1))))

    ;; integrate git with treemacs
    (use-package treemacs-magit
      :after (treemacs magit)
      :ensure t)

    ;; integrate projectile with treemacs
    (use-package treemacs-projectile
      :after (treemacs projectile)
      :ensure t)
  #+end_src
* Text Files
  Packages and configuration related to displaying, editing, and formatting text files.
** company
 #+begin_src emacs-lisp
   ;; company for text-completion
   (use-package company
     :config
     (global-company-mode))
  #+end_src
** flycheck
  #+begin_src emacs-lisp
    ;; flycheck for syntax checking
    (use-package flycheck
      :config
      (global-flycheck-mode))
  #+end_src
** hl-line
  #+begin_src emacs-lisp
    ;; highlight the current line
    (use-package hl-line
      :config
      (global-hl-line-mode +1))
  #+end_src
** paredit
  #+begin_src emacs-lisp
    (use-package paredit
      :config
      (add-hook 'emacs-lisp-mode-hook (lambda () (setq show-paren-style 'expression))))
  #+end_src
** rainbow-delimiters
  #+begin_src emacs-lisp
    (use-package rainbow-delimiters
      :config
      (add-hook 'emacs-lisp-mode-hook #'rainbow-delimiters-mode)
      (add-hook 'java-mode-hook #'rainbow-delimiters-mode)
      (add-hook 'rust-mode-hook #'rainbow-delimiters-mode))
  #+end_src
** format-all
   #+begin_src emacs-lisp
     (use-package format-all)
   #+end_src
** Formatting Configuration
   #+begin_src emacs-lisp
     ;; wraps visual lines
     (global-visual-line-mode)

     ;; newline at end of file
     (setq require-final-newline t)

     ;; wrap lines at 80 characters
     (setq-default fill-column 100)

     ;; delete trailing whitespace when saving.
     (add-hook 'before-save-hook 'delete-trailing-whitespace)

     ;; function for toggling comments
     (defun comment-or-uncomment-region-or-line ()
       "Comments or uncomments the region or the current line if there's no active region."
       (interactive)
       (let (beg end)
         (if (region-active-p)
             (setq beg (region-beginning) end (region-end))
           (setq beg (line-beginning-position) end (line-end-position)))
         (comment-or-uncomment-region beg end)
         (forward-line)))

     ;; binding toggle-comment to "C-/" for consistency with other editors
     (global-set-key (kbd "C-/") 'comment-or-uncomment-region-or-line)

     ;; function to untabify buffer
     (defun untabify-buffer ()
       (interactive)
       (untabify (point-min) (point-max)))

     ;; hydra for formatting files
     (defhydra hydra-formatting (:color blue)
       "formatting"
       ("f" format-all-buffer "format-all")
       ("u" untabify-buffer "untabify"))

   #+end_src
* Elfeed RSS Reader
  RSS reader using an org-mode file for configuration.
  #+begin_src emacs-lisp
    (use-package elfeed
      :bind ("C-c w" . elfeed)

      :config
      (use-package elfeed-org
        :init
        (elfeed-org)))
  #+end_src
* Nov EPub Reader
  EPub reader mode.
  #+begin_src emacs-lisp
    ;; epub reader mode
    (use-package nov
      :config
      (add-to-list 'auto-mode-alist '("\\.epub\\'" . nov-mode))
      :hook
      (nov-mode . visual-line-mode))
  #+end_src
* AUCTeX
  LaTeX editing enhancements.
  #+begin_src emacs-lisp
    (use-package auctex
      :defer t
      :hook (LaTeX-mode . reftex-mode)
      :custom
      (TeX-auto-save t)
      (TeX-byte-compile t)
      (TeX-clean-confirm nil)
      (TeX-master 'dwim)
      (TeX-parse-self t)
      (TeX-source-correlate-mode t)

      ;; pdf mode
      (TeX-PDF-mode t)
      (TeX-view-program-selection '((output-pdf "PDF Tools")))
      (TeX-view-program-list '(("PDF Tools" TeX-pdf-tools-sync-view)))
      (TeX-source-correlate-start-server t)

      (reftex-plug-into-AUCTeX t)
      (TeX-error-overview-open-after-TeX-run t)
      :config

      ;; formatting hooks
      (add-hook 'LaTeX-mode-hook 'visual-line-mode)
      (add-hook 'LaTeX-mode-hook 'flyspell-mode)
      (add-hook 'LaTeX-mode-hook 'LaTeX-math-mode)

      ;; overview of all compilation errors
      (setq TeX-error-overview-open-after-TeX-run t)

      ;; to have the buffer refresh after compilation
      (add-hook 'TeX-after-compilation-finished-functions
                #'TeX-revert-document-buffer))

    ;; reference management
    (use-package bibtex
      :after auctex
      :hook (bibtex-mode . my/bibtex-fill-column)
      :preface
      (defun my/bibtex-fill-column ()
        "Ensures that each entry does not exceed 120 characters."
        (setq fill-column 120)))

    ;; autocomplete support
    (use-package company-auctex
      :after (auctex company)
      :config (company-auctex-init))

    ;; enhanced pdf viewer
    (use-package pdf-tools
      :init
      (pdf-tools-install)
      :hook (pdf-view-mode . pdf-view-midnight-minor-mode))
  #+end_src
* Languages and LSP Support
  Packages and configuration related to language major/minor modes and language servers.
** LSP Mode
  #+begin_src emacs-lisp
    ;; lsp-mode plus other recommended packages and configuration
    (use-package lsp-mode
      :bind
      (:map lsp-mode-map
            (("C-M-b" . lsp-find-implementation)
             ("M-RET" . lsp-execute-code-action)))
      :config
      ;; completion provider
      (setq lsp-completion-provider :capf)
      (setq lsp-completion-enable t))

    ;; ui features for lsp-mode
    (use-package lsp-ui
      :after lsp-mode
      :bind
      ("M-i" . lsp-ui-imenu))

    ;; integration for lsp with ivy and treemacs
    (use-package lsp-ivy :commands lsp-ivy-workspace-symbol)
    (use-package lsp-treemacs :commands lsp-treemacs-errors-list)

    ;; debugging mode
    (use-package dap-mode
      :config
      (global-set-key (kbd "<f7>") 'dap-step-in)
      (global-set-key (kbd "<f8>") 'dap-net)
      (global-set-key (kbd "<f9>") 'dap-continue)
      (dap-mode t)
      (dap-ui-mode t)
      (dap-tooltip-mode 1)
      (tooltip-mode 1))

    ;; hydra bindings for language server commands.
    (defhydra hydra-lsp (:color amaranth)
      "Language Server Operations"

      ("f" lsp-format-buffer "format" :column "Buffer")
      ("m" lsp-ui-imenu "imenu")
      ("x" lsp-execute-code-action "execute action")

      ("M-r" lsp-restart-workspace "restart" :column "Server")
      ("S" lsp-shutdown-workspace "shutdown")
      ("M-s" lsp-describe-session "describe session")

      ("d" lsp-find-declaration "declaration" :column "Symbol")
      ("D" lsp-ui-peek-find-definitions "definition")
      ("R" lsp-ui-peek-find-references "references")
      ("i" lsp-ui-peek-find-implementation "implementation" :column "Symbol")
      ("t" lsp-find-type-definition "type")
      ("s" lsp-signature-help "signature")
      ("o" lsp-describe-thing-at-point "documentation" :column "Symbol")
      ("r" lsp-rename "rename")
      ("q" nil "exit" :color blue))
  #+end_src
** C
Needs clangd.
  #+begin_src emacs-lisp
    (add-hook 'c-mode-hook 'lsp)
  #+end_src
** Elisp
  #+begin_src emacs-lisp
    ;; Help for emacs-lisp functions
    (use-package eldoc
      :config
      (add-hook 'emacs-lisp-mode-hook 'turn-on-eldoc-mode)
      (add-hook 'lisp-interaction-mode-hook 'turn-on-eldoc-mode)
      (add-hook 'ielm-mode-hook 'turn-on-eldoc-mode))
  #+end_src
** Elixir
  #+begin_src emacs-lisp
    ;; Elixir major mode hooked up to lsp
    (use-package elixir-mode
      :hook (elixir-mode . lsp))

    ;; minor mode for mix commands
    (use-package mix
      :hook (elixir-mode mix-minor-mode))
  #+end_src
** OCaml
  #+begin_src emacs-lisp
    ;; OCaml major mode
    (use-package tuareg
      :hook (tuareg-mode . lsp))

    ;; dune integration, don't know how to use
    (use-package dune)
  #+end_src
** Java
  #+begin_src emacs-lisp
    (use-package lsp-java
      :hook (java-mode . lsp)
      :config
      ;; debugging
      (require 'dap-java))

    ;; function to build jar from maven project
    (defun mvn-jar ()
      "Packages the maven project into a jar."
      (interactive)
      (mvn "package"))

    ;; function to run the main class defined for the maven project
    (defun mvn-run ()
      "Run the maven project using the exec plugin."
      (interactive)
      (mvn "compile exec:java"))

    ;; function to test all test classes
    (defun mvn-test-all ()
      "Run all test classes in the maven project."
      (interactive)
      (mvn "test"))

    ;; maven minor mode
    (use-package mvn
      :bind
      (:map java-mode-map
            (("C-c M" . mvn)
             ("C-c m r" . mvn-run)
             ("C-c m c" . mvn-compile)
             ("C-c m T" . mvn-test) ;; asks for specific test class to run
             ("C-c m t" . mvn-test-all)
             ("C-c m j" . mvn-jar))))
   #+end_src
** Python
  #+begin_src emacs-lisp
		(require 'lsp-pyright) ;; not sure if this does anything

		;; this registers it, but the server does not get past "starting"
		(lsp-register-client
		 (make-lsp-client :new-connection (lsp-tramp-connection "pyls")
											:major-modes '(python-mode)
											:remote? t
											:server-id 'pyls-remote))

		(use-package python
			:hook
			(python-mode . lsp)
			:config
			(cond
			 ;; i use python3
			 ((executable-find "python3")
				(setq python-shell-interpreter "python3"))))
  #+end_src
** Rust
  #+begin_src emacs-lisp
    ;; hook up rust-mode with the language server
    (use-package rust-mode
      :config
      (setq rust-format-on-save t)
      ;;(add-hook 'rust-mode-hook (lambda () (local-unset-key (kbd "C-c C-d")))) ;; interferes with my custom keybinding for copying a line and I don't ever use the rust-mode binding
      :hook (rust-mode . lsp))

    ;; cargo minor mode for cargo keybindings
    (use-package cargo
      :hook (rust-mode . cargo-minor-mode))
  #+end_src
** sh
  #+begin_src emacs-lisp
    (add-hook 'shell-mode-hook
        (lambda ()
          (setq sh-basic-offset 2
          shr-indentation 2)))
  #+end_src
** Web Dev
Currently not doing web development, by my estimation I will need these basic packages.
   #+begin_src emacs-lisp
     (use-package web-mode)
     (use-package typescript-mode)
     (use-package tide)
   #+end_src
* Org Mode
  Configuration for the majestic org-mode.
** General
  #+begin_src emacs-lisp
    (setq org-directory "~/Org")

    ;; bullets instead of asterisks
    (use-package org-bullets
      :hook (org-mode . org-bullets-mode))

    ;; org src blocks act more like the major mode
    (setq org-src-fontify-natively t)
    (setq org-src-tab-acts-natively t)

    ;; editing source block in same window
    (setq org-src-window-setup 'current-window)

    ;; for the "old-school" <s-<tab> to make src blocks
    (require 'org-tempo)
    (add-to-list 'org-structure-template-alist '("el" . "src emacs-lisp"))

    ;; change tabs from org-mode
    (with-eval-after-load 'org
      (define-key org-mode-map [(control tab)] 'tab-bar-switch-to-next-tab))

    (setq org-support-shift-select t)
    (setq org-replace-disputed-keys t)
  #+end_src
** hydra-org
   #+begin_src emacs-lisp
     (defhydra hydra-org (:color blue)
       "orgmode"
       ("c" org-capture "capture")
       ("a" org-agenda "agenda")
       ("p" org-projectile-project-todo-completing-read "projectile"))
   #+end_src
** org-capture
   #+begin_src emacs-lisp
     (global-set-key (kbd "C-c c") 'org-capture)

     ;; abstracted template for a TODO to take place on some day, like an assignment due date.
     (defun temp/todo-on-day ()
       "* TODO %? %^t")

     (setq org-capture-templates
           '(("c" "Class Task")
             ("cs" "Systems Task" entry
              (file+headline "class-tasks.org" "Systems")
              (function temp/todo-on-day))
             ("cn" "Networks Task" entry
              (file+headline "class-tasks.org" "Networks")
              (function temp/todo-on-day))
             ("cc" "Cyber Task" entry
              (file+headline "class-tasks.org" "Cyber")
              (function temp/todo-on-day))
             ("cp" "Phonology Task" entry
              (file+headline "class-tasks.org" "Phonology")
              (function temp/todo-on-day))))
   #+end_src
** org-agenda
   #+begin_src emacs-lisp
     (setq org-agenda-files (append org-agenda-files '("~/Org")))
   #+end_src
** org-present
   #+begin_src emacs-lisp
     (use-package org-present
       :config
       ;; from the github page. "C-c C-q" to quit.
       (eval-after-load "org-present"
       '(progn
          (add-hook 'org-present-mode-hook
                    (lambda ()
                      (org-present-big)
                      (org-display-inline-images)
                      (org-present-hide-cursor)
                      (org-present-read-only)))
          (add-hook 'org-present-mode-quit-hook
                    (lambda ()
                      (org-present-small)
                      (org-remove-inline-images)
                      (org-present-show-cursor)
                      (org-present-read-write))))))
   #+end_src
** org-projectile
   #+begin_src emacs-lisp
     ;; put a todo file in the directory of each projectile project and link them to org-agenda
     (use-package org-projectile
       :config
       (setq org-projectile-per-filepath "todo.org"
             org-agenda-files (append org-agenda-files (org-projectile-todo-files)))
       (org-projectile-per-project))
   #+end_src
* EXWM
  Configuration for using emacs as an X window manager.
  #+begin_src emacs-lisp
    ;; should exwm be enabled?
    (setq exwm-enabled (and (eq window-system 'x)
                            (seq-contains command-line-args "--use-exwm")))

    (use-package exwm
      :if exwm-enabled
      :config
      ;; package to manage bluetooth from emacs
      (use-package bluetooth)

      ;; mode to bind media keys
      (use-package desktop-environment
        :config
        ;; for some reason the default volume commands do not work
        (setq desktop-environment-volume-toggle-command       "amixer -D pulse set Master toggle"
              desktop-environment-volume-set-command          "amixer -D pulse set Master %s"
              desktop-environment-volume-get-command          "amixer -D pulse get Master"
              ;; brightness change amount
              desktop-environment-brightness-normal-increment "5%+"
              desktop-environment-brightness-normal-decrement "5%-"
              desktop-environment-brightness-small-increment  "2%+"
              desktop-environment-brightness-small-decrement  "2%-"
              )
        (desktop-environment-mode))

      (setq exwm-workspace-number 2)
      (setq exwm-randr-workspace-monitor-plist
            '(0 "eDP-1" ;; laptop
                1 "DP-3")) ;; external monitor via HDMI which is for some reason named DP-3

      ;; when window "class" updates, use it to set the buffer name
      (defun exwm-update-class ()
        (exwm-workspace-rename-buffer exwm-class-name))
      (add-hook 'exwm-update-class-hook #'exwm-update-class)

      ;; these keys should always pass through to emacs
      (setq exwm-input-prefix-keys
            '(?\C-x
              ?\C-u
              ?\C-h
              ?\C-g
              ?\M-x
              ?\M-!))

      ;; enable the next key to be sent directly, for things like copy and paste from x windows
      (define-key exwm-mode-map [?\C-m] 'exwm-input-send-next-key)

      ;; set up global key bindings.  these always work, no matter the input state!
      ;; keep in mind that changing this list after EXWM initializes has no effect.
      (setq exwm-input-global-keys
            `(
              ;; reset to line-mode (C-c C-k switches to char-mode via exwm-input-release-keyboard)
              ([?\s-r] . exwm-reset)

              ;; general app launcher
              ([?\s-/] . (lambda ()
                           (interactive)
                           (counsel-linux-app)))

              ;; shortcut for firefox
              ([?\s-x] . (lambda ()
                           (interactive)
                           (shell-command "firefox")))

              ;; shortcut for terminal emulator
              ([s-return] . (lambda ()
                              (interactive)
                              (vterm-other-window)))

              ;; switch workspace
              ;;([?\s-w] . exwm-workspace-switch)

              ;; 's-N': switch to certain workspace with super (win) plus a number key (0 - 9)
              ;; ,@(mapcar (lambda (i)
              ;;            `(,(kbd (format "s-%d" i)) .
              ;;              (lambda ()
              ;;                (interactive)
              ;;                (exwm-workspace-switch-create ,i))))
              ;;          (number-sequence 0 9))
              )))

    ;; function to turn on all the exwm stuff
    (defun enable-exwm ()
      "Enables the features of EXWM."

      ;; ensure screen updates with xrandr will refresh EXWM frames
      (require 'exwm-randr)
      (exwm-randr-enable)

      ;; use default super+shift keybindings
      (windmove-swap-states-default-keybindings)

      ;; remap capsLock to ctrl
      (start-process-shell-command "xmodmap" nil "xmodmap ~/.emacs.d/exwm/Xmodmap")

      ;; display time
      (setq display-time-default-load-average nil)
      (display-time-mode t)

      ;; Show battery status in the mode line
      (display-battery-mode 1)

      (exwm-enable)
      (exwm-init))

    (if exwm-enabled (enable-exwm) ())
  #+end_src
